plugins {
    id "java"
    id "java-library"
    id "com.diffplug.spotless" version "6.10.0"
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.resources.srcDirs = ["../assets"]

project.ext.mainClassName = "io.github.fourlastor.game.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

spotless {
    enforceCheck = false
    java {
        palantirJavaFormat()
    }
}

task dist(type: Jar) {
    dependsOn classes
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dependencies {
    implementation project(":core")
    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
}
