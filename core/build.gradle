plugins {
    id "java-library"
    id "com.diffplug.spotless" version "6.10.0"
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

spotless {
    enforceCheck = false
    java {
        palantirJavaFormat()
    }
}

tasks.withType(JavaCompile) {
    options.incremental = false
}

task packTextures {
    def inputDir = "$rootDir/assets/images/included"
    def outputDir = "$rootDir/assets/images/included/packed"
    inputs.dir inputDir
    outputs.dir outputDir
    doLast {
        delete(outputDir)
        TexturePacker.process(inputDir, outputDir, "images.pack")
    }
}

project.tasks.compileJava.dependsOn packTextures

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
    api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    api "com.badlogicgames.ashley:ashley:1.7.4"
    implementation "com.github.tommyettinger:textratypist:0.7.2"
    implementation "com.google.dagger:dagger:$daggerVersion"
    api "com.google.dagger:dagger-gwt:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
}
